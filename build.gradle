plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.20'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'jacoco'
}

group 'com.github.tenebras'
version '0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlinx:kotlinx-cli:0.3.5"

    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.9.2"
    testImplementation "io.mockk:mockk:1.13.5"
}

test {
    useJUnitPlatform()

    finalizedBy(jacocoTestReport)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(18)
    }
}

kotlin {
    kotlinDaemonJvmArgs = ["-Xmx1500m", "-Xms500m", "-XX:+UseParallelGC"]
}

compileKotlin {
    kotlinOptions.jvmTarget = "18"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "18"
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
            'Main-Class': 'com.github.tenebras.minipb.MainKt'
        )
    }

    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'com.github.tenebras.minipb'
        property 'sonar.sources', 'src/main/kotlin'
        property 'sonar.tests', 'src/test/kotlin'
    }
}

jacoco {
    toolVersion = "0.8.8"
    jacocoTestReport {
        reports {
            xml.getRequired().set(true)
        }
    }
}